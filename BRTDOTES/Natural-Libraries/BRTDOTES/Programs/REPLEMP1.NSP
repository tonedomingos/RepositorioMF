* >Natural Source Header 000000
* :Mode S
* :CP windows-1252
* :LineIncrement 10
* <Natural Source Header
DEFINE DATA LOCAL
01 V-EMP VIEW OF EMPL-BRZ-DB204
  02 PERSONNEL-ID
  02 FIRST-NAME
  02 MIDDLE-NAME
  02 NAME
  02 MAR-STAT
  02 DEPT
  02 JOB-TITLE
01 #LOOP        (A01)    INIT <'Y'>
01 #P-ID        (A08)    INIT <'20002000'>
01 #NAME        (A20)
01 #M-NAME      (A20)
01 #F-NAME      (A20)
01 #MAR-S       (A01)
01 #DEPT        (A06)
01 #JOB-T       (A25)
END-DEFINE
*
SET KEY PF3='EXIT'
*
REPEAT WHILE #LOOP = 'Y'
*
  INPUT // 'EMP UPDATE - PERSONNELID:' #P-ID (AD=MILT)
  IF *PF-KEY = 'PF3' OR #P-ID = ' '
    MOVE 'N'   TO  #LOOP
  ELSE
    RESET #DEPT #JOB-T #MAR-S
    FIND V-EMP WITH PERSONNEL-ID = #P-ID
      MOVE FIRST-NAME  TO #F-NAME
      MOVE MIDDLE-NAME TO #M-NAME
      MOVE NAME        TO #NAME
      MOVE MAR-STAT    TO #MAR-S
      MOVE DEPT        TO #DEPT
      MOVE JOB-TITLE   TO #JOB-T
      INPUT // 'UPDATE PERSONNELID:' #P-ID (AD=O)          ////
        '              NAME       :' #NAME (AD=MILT)       //
        '              FIRST-NAME :' #F-NAME (AD=MILT)     //
        '              MIDDLE-NAME:' #M-NAME (AD=MILT)     //
        '              MAR-STATUS :' #MAR-S (AD=MILT)      //
        '              DEPT       :' #DEPT (AD=MILT)       //
        '              JOB-TITLE  :' #JOB-T (AD=MILT)
      MOVE #F-NAME   TO FIRST-NAME
      MOVE #M-NAME   TO MIDDLE-NAME
      MOVE #NAME     TO NAME
      MOVE #DEPT     TO DEPT
      MOVE #MAR-S    TO MAR-STAT
      MOVE #JOB-T    TO JOB-TITLE
      UPDATE
      END TRANSACTION
    END-FIND
  END-IF
END-REPEAT
*
END  /* PROGRAM */
